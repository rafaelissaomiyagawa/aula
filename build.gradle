plugins {
    id 'java'
    id 'java-test-fixtures'
    alias(libs.plugins.spring.boot)
    alias(libs.plugins.spring.dependency.management)
    alias(libs.plugins.lombok)
    alias(libs.plugins.test.logger)
    alias(libs.plugins.spotless)
    alias(libs.plugins.versions)
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.kafka:spring-kafka'
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // JPA + JDBC Template
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly 'org.postgresql:postgresql'

    // Flyway
    implementation libs.flyway.core
    implementation libs.flyway.database.postgresql

    // Legal! executa quando subimos o spring boot com ./gradlew bootRun
    // Mas não faz a mesma coisa no ambiente de teste de integração!
    developmentOnly 'org.springframework.boot:spring-boot-docker-compose'

    // Test Fixture dependencies
    // Spring Boot testing support
    testFixturesImplementation 'org.springframework.boot:spring-boot-starter-test'
    // Random test data generator for JUnit
    testFixturesImplementation libs.instancio.junit
}

tasks.named("test", Test) {
    useJUnitPlatform()
}

// Não gera relatório no dev. local
// Se gerar, está no build/reports/tests
if (!project.hasProperty("ci-environment")) {
    test {
        reports {
            html.required = false
            junitXml.required = false
        }
    }
}

// https://github.com/radarsh/gradle-test-logger-plugin
testlogger {
    showPassed = true
    showFailed = true
    showSkipped = false
}


// https://docs.gradle.org/current/userguide/jvm_test_suite_plugin.html
testing {
    suites {
        configureEach { suite ->
            suite.dependencies {
                implementation testFixtures(project())

                // Spring Boot testing support
                implementation 'org.springframework.boot:spring-boot-starter-test'

                // Random test data generator for JUnit
                implementation libs.instancio.junit
            }
        }

        // Built-in test suite (automatically created by Java plugin)
        test {

        }

        // Custom test suite for integration tests
        integrationTest(JvmTestSuite) {
            // Dependencies for integration tests
            dependencies {
                // Include the main project code as a dependency
                // (test suite automatically has access to test sources)
                implementation project()
                // Bill of Materials
                // Tem as versoes das dependencias!
                implementation platform(libs.testcontainers.bom)
                implementation 'org.testcontainers:junit-jupiter'
                implementation 'org.testcontainers:postgresql'
                implementation 'org.springframework.boot:spring-boot-testcontainers'
                implementation 'org.springframework.kafka:spring-kafka-test'
            }

            // Configuration for the test task that runs these tests
            targets {
                configureEach {  // Applies to all targets (currently just one task)
                    testTask.configure {
                        systemProperty 'spring.profiles.active', 'integration'
                        // Ordering rule: integrationTest runs after regular test task
                        shouldRunAfter(test)
                        filter {
                            excludeTestsMatching '*CanaryTest*'
                        }
                    }
                }
            }
        }


    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
}

// Register a task for canary test
tasks.register('canaryTest', Test) {
    description = 'Runs canary test to validate environment'
    group = 'verification'

    // Use integration test source set
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    systemProperty 'spring.profiles.active', 'integration'

    // Only run CanaryTest - use JUnit pattern
    useJUnitPlatform {
        includeEngines 'junit-jupiter'
    }

    // Include only CanaryTest
    filter {
        includeTestsMatching '*CanaryTest'
    }
}

// Hook integration tests into the standard check lifecycle
tasks.named('check') {
    // Makes running 'check' also run integration tests
    dependsOn(testing.suites.integrationTest)
}

tasks.named('integrationTest') {
    dependsOn canaryTest
}

spotless {
    java {
        googleJavaFormat()
    }
}
