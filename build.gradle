plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.8'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'io.freefair.lombok' version '8.6'
    id 'com.adarshr.test-logger' version '4.0.0'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // JPA + JDBC Template
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly 'org.postgresql:postgresql'

    // Legal! executa quando subimos o spring boot com ./gradlew bootRun
    // Mas não faz a mesma coisa no ambiente de teste de integração!
    developmentOnly("org.springframework.boot:spring-boot-docker-compose")
}

tasks.named("test", Test) {
    useJUnitPlatform()
}

// Não gera relatório no dev. local
// Se gerar, está no build/reports/tests
if (!project.hasProperty("ci-environment")) {
    test {
        reports {
            html.required = false
            junitXml.required = false
        }
    }
}

// https://github.com/radarsh/gradle-test-logger-plugin
testlogger {
    showPassed = false
    showFailed = true
    showSkipped = false
}


// https://docs.gradle.org/current/userguide/jvm_test_suite_plugin.html
testing {
    suites {
        test {
            dependencies {
                implementation 'org.springframework.boot:spring-boot-starter-test'

                // Facilitador para criar massa de dados aleatório
                // https://www.instancio.org/user-guide/
                implementation 'org.instancio:instancio-junit:5.5.1'
            }
        }
    }
}
